--- start of standard stuff ---
data Maybe X = nothing | just X
data Pair X Y = pair X Y

reverse' : {List X -> List X -> List X}
reverse' []        ys = ys
reverse' (x :: xs) ys = reverse' xs (x :: ys)

reverse : {List X -> List X}
reverse xs = reverse' xs []

map : {{X -> Y} -> List X -> List Y}
map f []        = []
map f (x :: xs) = f x :: map f xs

print : {String -> [Console]Unit}
print s = map ouch s; unit
--- end of standard stuff ---

-- =============================================================================
-- Description
-- Little Frank library for writing concurrent programs that can synchronise via
-- promises. When a new Async process is spawned, the caller gets a promise
-- object. When the caller wants to redeem the given promise, it `awaits`
-- the promise and blocks until the spawned process is executed.
-- =============================================================================

--------------------------------------------------------------------------------
-- Queue interface and fast queue implementation
--------------------------------------------------------------------------------

interface Queue S = enqueue : S -> Unit
                  | dequeue : Maybe S

-- Execute a computation along with a queue as state (generalisation of
-- zipQueue from coop-factored.fk)
data ZipQueue S = zipq (List S) (List S)

runZipQueue: {{[Queue S]X} -> ZipQueue S -> Pair X (ZipQueue S)}
runZipQueue t q = runZipQueue' t! q

runZipQueue': {<Queue S>X -> ZipQueue S -> Pair X (ZipQueue S)}
runZipQueue' <enqueue q -> k> (zipq ps        qs) = runZipQueue' (k unit)     (zipq ps (q :: qs))
runZipQueue' <dequeue -> k>   (zipq []        []) = runZipQueue' (k nothing)  (zipq [] [])
runZipQueue' <dequeue -> k>   (zipq []        qs) = runZipQueue' (k dequeue!) (zipq (reverse qs) [])
runZipQueue' <dequeue -> k>   (zipq (p :: ps) qs) = runZipQueue' (k (just p)) (zipq ps qs)
runZipQueue' x                (zipq ps        qs) = pair x (zipq ps qs)

evalZipQueue: {{[Queue S]X} -> ZipQueue S -> X}
evalZipQueue t q = case (runZipQueue t q) { (pair x _) -> x }

execZipQueue: {{[Queue S]X} -> ZipQueue S -> ZipQueue S}
execZipQueue t q = case (runZipQueue t q) { (pair _ q) -> q }

--------------------------------------------------------------------------------
-- Definitions of interfaces, data types
--------------------------------------------------------------------------------

interface Co = fork : {[Co]Unit} -> Unit
             | yield : Unit

data PromiseStatus X = done X
                     | waiting (List {X -> Unit})

data Promise X = promise (Ref (PromiseStatus X))

interface Async = async X: {[Async]X} -> Promise X
                | await X: Promise X -> X

--------------------------------------------------------------------------------
-- Transform an async process into fork/yield process
--------------------------------------------------------------------------------

step: {Promise X [RefState, Co[RefState]] -> <Async[RefState, Co[RefState]]>X -> [RefState, Co[RefState]]Unit}
step (promise ref) v = write ref (done v)                         -- fulfill promise
step p <async c -> k> = let ref' = new (waiting []) in            -- new blank promise
                        let job = {step (promise ref') c!} in     -- new job which fulfills promise
                        fork { execIfNeeded (promise ref') job }; -- push job
                        step p (k (promise ref'))                 -- continue with given promise
step p <await (promise ref') -> k> = case (read ref')             -- lookup status of promise
                                     { (done v) -> step p (k v)   -- if fulfilled, continue with value
                                     | (waiting js) ->            -- else, add itself to promise's waiting list
                                         let job = { v -> step p (k v) } in
                                         write ref' (waiting (job::js)) }

execIfNeeded: {Promise X [RefState, Co[RefState]] -> {[RefState, Co[RefState]]Unit} -> [RefState, Co[RefState]]Unit}
execIfNeeded (promise ref) f = case (read ref)             -- lookup status of promise
                               { (done _) -> unit          -- invariant: should not happen
                               | (waiting []) -> yield!;   -- if noone is waiting for it, idle
                                                 execIfNeeded (promise ref) f
                               | (waiting fs) -> f!;       -- if there is demand, execute
                                                 case (read ref)
                                                 { (done v) -> map { f -> f v } fs; unit -- wake up and feed value
                                                 | _ -> unit } }                         -- invariant: should not happen

--------------------------------------------------------------------------------
-- This is the process executer from coop-factored.fk, using a queue to handle
-- multiple processes
--------------------------------------------------------------------------------

data Proc = proc {[Queue Proc]Unit}

pushProc : {{[Queue Proc]Unit} -> [Queue Proc]Unit}
pushProc p = enqueue (proc p)

popProc : {[Queue Proc]Maybe Unit}
popProc! = case dequeue! { (just (proc x)) -> just x!
                         | nothing         -> nothing }

popProcs : {[Queue Proc]Unit}
popProcs! = case popProc! { (just unit) -> popProcs!
                          | nothing     -> unit }

-- Serialise a queue-tree and return a "queue-manager" in the following way:
-- Given a forkable computation,
-- 1) yield  ==translate==>  push continuation into queue, pop rest of list
-- 2) fork   ==translate==>  push fork into queue, then carry on
-- 3) unit   ==translate==>  pop rest of list
-- roundRobin : {<Co [R | Queue Proc]>Unit -> [R | Queue Proc]Unit}
roundRobin : {<Co [Queue Proc]>Unit -> [Queue Proc]Unit}
roundRobin <yield -> k>  = pushProc {roundRobin (k unit)}; popProcs!
roundRobin <fork p -> k> = pushProc {roundRobin p!}; roundRobin (k unit)
roundRobin unit          = popProcs!

--------------------------------------------------------------------------------
-- Concurrent Async programs
--------------------------------------------------------------------------------

binOp : {Int -> Int -> [Async]Int}
binOp m n = m + n

calcProg : {Int -> Int -> Int -> [Async]Int}
calcProg a b c = let p1 = async {binOp a b} in
                 let p2 = async {binOp b c} in
                 let v1 = await p1 in
                 let v2 = await p2 in
                 v1 + v2

--------------------------------------------------------------------------------
-- Main
--------------------------------------------------------------------------------

-- Dont delete these lines, they make this program into a unit test.
-- #desc   examples/asyncPoly.fk
-- #return 8

main: {[RefState]Int}
main! = let resRef = new (waiting []) in
        runZipQueue {roundRobin (step (promise resRef) (calcProg 1 2 3))} (zipq [] []);
        case (read resRef)
        { (done v) -> v
        | _ -> 0 }  -- invariant: should not happen
